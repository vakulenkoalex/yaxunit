//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2024 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТест("Инициализировать")
		.ДобавитьТест("Добавить")
		.ДобавитьТест("MoveFirst")
		.ДобавитьТест("MoveNext")
		.ДобавитьТест("Close")
		.ДобавитьТест("Fields")
	;
	
КонецПроцедуры

Процедура Инициализировать() Экспорт
	
	Набор = ЮТест.Данные().ADORecordSet("Колонка1, Колонка2");
	
	ЮТест.ОжидаетЧто(Набор)
		.Свойство("EOF").ЭтоИстина()
		.Свойство("BOF").ЭтоИстина()
		.Свойство("AbsolutePosition").Равно(0)
		.Свойство("RecordCount").Равно(0)
		.Свойство("Fields.Count").Равно(2)
	
КонецПроцедуры

Процедура Добавить() Экспорт
	
	Набор = ЮТест.Данные().ADORecordSet("Колонка1, Колонка2");
	
	ЮТест.ОжидаетЧто(Набор, "Инициализированный набор")
		.Свойство("EOF").ЭтоИстина()
		.Свойство("RecordCount").Равно(0);
	
	ЮТест.ОжидаетЧто(Набор.Добавить(1, 2), "Набор с одной записью")
		.Свойство("EOF").ЭтоЛожь()
		.Свойство("RecordCount").Равно(1);
	
	ЮТест.ОжидаетЧто(Набор.Добавить(2, 1), "Набор с двумя записями")
		.Свойство("EOF").ЭтоЛожь()
		.Свойство("RecordCount").Равно(2);
	
КонецПроцедуры

Процедура MoveFirst() Экспорт
	
	Набор = ЮТест.Данные().ADORecordSet("Колонка1, Колонка2");
	
	Набор.Добавить(1, 2);
	Набор.Добавить(3, 4);
	
	Набор.MoveNext();
	Набор.MoveNext();
	Набор.MoveNext();
	ЮТест.ОжидаетЧто(Набор, "После сдвига в конец")
		.Свойство("AbsolutePosition").Равно(3)
		.Свойство("EOF").ЭтоИстина();
	Набор.MoveFirst();
	ЮТест.ОжидаетЧто(Набор, "После сдвига в начало")
		.Свойство("AbsolutePosition").Равно(0)
		.Свойство("EOF").ЭтоЛожь();
	
КонецПроцедуры

Процедура MoveNext() Экспорт
	
	Набор = ЮТест.Данные().ADORecordSet("Колонка1, Колонка2");
	
	Набор.Добавить(1, 2);
	Набор.Добавить(3, 4);
	
	Для Инд = 1 По 4 Цикл
		Набор.MoveNext();
		ЮТест.ОжидаетЧто(Набор, "После сдвига " + Инд)
			.Свойство("AbsolutePosition").Равно(Инд)
			.Свойство("EOF").Равно(Инд >= 2);
	КонецЦикла;
	
КонецПроцедуры

Процедура Close() Экспорт
	
	Набор = ЮТест.Данные().ADORecordSet("Колонка1, Колонка2");
	
	Набор.Добавить(1, 2);
	
	Набор.Close();
	ЮТест.ОжидаетЧто(Набор)
		.Свойство("EOF").ЭтоИстина()
		.Свойство("BOF").ЭтоИстина()
		.Свойство("AbsolutePosition").Равно(0)
		.Свойство("RecordCount").Равно(0)
		.Свойство("Fields.Count").Равно(0)
	
КонецПроцедуры

Процедура Fields() Экспорт
	
	Набор = ЮТест.Данные().ADORecordSet("Колонка1, Колонка2");
	
	ЮТест.ОжидаетЧто(Набор)
		.Свойство("Fields.Count").Равно(2);
	
	ЮТест.ОжидаетЧто(Набор.Fields(0))
		.Свойство("Name").Равно("Колонка1")
		.Свойство("Value").Равно(Неопределено);
	ЮТест.ОжидаетЧто(Набор.Fields(1))
		.Свойство("Name").Равно("Колонка2")
		.Свойство("Value").Равно(Неопределено);
	
	Набор.Добавить(1, 2);
	Набор.Добавить(3, 4);
	
	ЮТест.ОжидаетЧто(Набор.Fields(0))
		.Свойство("Value").Равно(1);
	ЮТест.ОжидаетЧто(Набор.Fields(1))
		.Свойство("Value").Равно(2);
	
	Набор.MoveNext();
	ЮТест.ОжидаетЧто(Набор.Fields(0))
		.Свойство("Value").Равно(3);
	ЮТест.ОжидаетЧто(Набор.Fields(1))
		.Свойство("Value").Равно(4);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
